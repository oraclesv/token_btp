import "util.scrypt";
import "swapUtil.scrypt";
import "rabinUtil.scrypt";
import "txUtil.scrypt";

contract SwapRemoveLiquidity {
    bytes swapContractID;
    bytes tokenID;
    bytes lpTokenID;
    int[3] rabinPubKeyArray;

    public function unlock(
        SigHashPreimage txPreimage, 
        bytes prevouts,
        int tokenInputIndex,
        bytes tokenLockingScript, // token input locking script 
        bytes tokenRabinMsg,
        bytes[3] tokenPaddingArray,
        int[3] tokenSigArray,
        int lpTokenInputIndex,
        bytes lpTokenLockingScript,
        bytes lpTokenRabinMsg,
        bytes[3] lpTokenPaddingArray,
        int[3] lpTokenSigArray,
        int swapInputIndex,
        bytes swapLockingScript, // swap input locking script
        bytes swapRabinMsg,
        bytes[3] swapPaddingArray,
        int[3] swapSigArray,
        PubKey senderPubKey, 
        Sig senderSig, 
        bytes lpMerklePath,
        int changeSatoshis
    ) {
        require(Tx.checkPreimage(txPreimage));

        // check prevouts
        require(hash256(prevouts) == Util.hashPrevouts(txPreimage));

        // check sender sig
        require(checkSig(senderSig, senderPubKey));

        Ripemd160 sender = ripemd160(sha256(senderPubKey));

        require(changeSatoshis >= 0);

        // verify swap contract
        int swapInputSatoshis = RabinUtil.getMsgOutputSatoshis(swapRabinMsg);
        require(RabinUtil.checkOutputPoint(prevouts, swapInputIndex, swapRabinMsg));
        require(hash160(swapLockingScript) == RabinUtil.getMsgLockingScriptHash(swapRabinMsg));

        // verify the swap contract ID
        require(this.swapContractID == RabinUtil.getMsgUniqueID(swapRabinMsg));

        int swapVerifyCount = RabinUtil.verifyMsgThree(this.rabinPubKeyArray, swapRabinMsg, swapPaddingArray, swapSigArray);
        require(swapVerifyCount >= 2);

        int scriptLen = len(swapLockingScript);
        int poolTokenAmount = SwapUtil.getTokenAmount(swapLockingScript, scriptLen);
        int totalLpAmount = SwapUtil.getLpAmount(swapLockingScript, scriptLen);

        // verify token input
        require(RabinUtil.checkOutputPoint(prevouts, tokenInputIndex, tokenRabinMsg));
        require(hash160(tokenLockingScript) == RabinUtil.getMsgLockingScriptHash(tokenRabinMsg));
        require(this.tokenID == RabinUtil.getMsgTokenID(tokenRabinMsg));

        int tokenVerifyCount = RabinUtil.verifyMsgThree(this.rabinPubKeyArray, tokenRabinMsg, tokenPaddingArray, tokenSigArray);
        require(tokenVerifyCount >= 2);

        int tokenInputSatoshis = RabinUtil.getMsgOutputSatoshis(tokenRabinMsg);
        int tokenInputAmount = RabinUtil.getMsgTokenAmount(tokenRabinMsg);

        // verify lp token input
        require(RabinUtil.checkOutputPoint(prevouts, lpTokenInputIndex, lpTokenRabinMsg));
        require(hash160(lpTokenLockingScript) == RabinUtil.getMsgLockingScriptHash(lpTokenRabinMsg));
        require(this.lpTokenID == RabinUtil.getMsgTokenID(lpTokenRabinMsg));

        int lpTokenVerifyCount = RabinUtil.verifyMsgThree(this.rabinPubKeyArray, lpTokenRabinMsg, lpTokenPaddingArray, lpTokenSigArray);
        require(lpTokenVerifyCount >= 2);

        int lpTokenInputSatoshis = RabinUtil.getMsgOutputSatoshis(lpTokenRabinMsg);
        int lpTokenInputAmount = RabinUtil.getMsgTokenAmount(lpTokenRabinMsg);

        // change the lp amount and calculate new lp merkle tree
        int newTotalLpAmount = totalLpAmount - lpTokenInputAmount;

        // change token amount and calculate new token amount
        int bsvAdd = lpTokenInputAmount * swapInputSatoshis / totalLpAmount;
        int tokenAdd = lpTokenInputAmount * poolTokenAmount / totalLpAmount;
        require(tokenAdd <= tokenInputAmount);

        int swapOutputSatoshis = swapInputSatoshis - bsvAdd;

        require(poolTokenAmount >= tokenAdd);
        int newPoolTokenAmount = poolTokenAmount - tokenAdd;

        // swap contract output
        bytes newSwapScript = SwapUtil.genNewSwapContract(swapLockingScript, newPoolTokenAmount, newTotalLpAmount);
        bytes swapOutput = Util.buildOutput(newSwapScript, swapOutputSatoshis);

        // token output
        int tokenLockingScriptLen = len(tokenLockingScript);
        bytes newTokenScript = TokenProto.getNewTokenScript(tokenLockingScript, tokenLockingScriptLen, sender, tokenAdd);
        bytes tokenOutput = Util.buildOutput(newTokenScript, tokenInputSatoshis);

        // change token
        int leftTokenAmount = tokenInputAmount - tokenAdd;
        bytes tokenChangeOutput = b'';
        if (leftTokenAmount > 0) {
            bytes address = TokenProto.getTokenAddress(tokenLockingScript, tokenLockingScriptLen);
            bytes newTokenScript2 = TokenProto.getNewTokenScript(tokenLockingScript, tokenLockingScriptLen, address, leftTokenAmount);
            tokenChangeOutput = Util.buildOutput(newTokenScript2, tokenInputSatoshis);
        }

        bytes changeOutput = b'';
        if (changeSatoshis > 0) {
            bytes outputScript = Util.buildPublicKeyHashScript(sender);
            changeOutput = Util.buildOutput(outputScript, changeSatoshis);
        }
        
        bytes outputs = swapOutput + tokenOutput + tokenChangeOutput + changeOutput;
        require(hash256(outputs) == Util.hashOutputs(txPreimage));
    }
}